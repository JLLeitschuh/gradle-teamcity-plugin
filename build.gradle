
plugins {
    id 'org.gradle.groovy'
    id 'org.gradle.java-gradle-plugin'
    id 'org.gradle.jacoco'
    id 'org.gradle.maven-publish'
    id 'com.gradle.plugin-publish' version '0.9.8'
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'de.undercouch:gradle-download-task:3.2.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.xmlunit:xmlunit-matchers:2.3.0'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:2.8.9'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

version = '1.1-SNAPSHOT'
group = 'com.github.rodm'

sourceSets {
    functionalTest {
        compileClasspath += main.output + configurations.testRuntime
        runtimeClasspath += main.output + configurations.testRuntime
        groovy {
            srcDir 'src/functional/groovy'
        }
    }
    samples {
        compileClasspath += main.output + configurations.testRuntime
        runtimeClasspath += main.output + configurations.testRuntime
    }
}

gradlePlugin {
    testSourceSets sourceSets.functionalTest, sourceSets.samples
}

task functionalTest(type: Test) {
    description = 'Runs the functional tests.'
    group = 'verification'
    testClassesDir = sourceSets.functionalTest.output.classesDir
    classpath = sourceSets.functionalTest.runtimeClasspath

    reports {
        html.destination = project.file("$html.destination/functional")
        junitXml.destination = project.file("$junitXml.destination/functional")
    }
}

task samplesTest(type: Test, dependsOn: 'publish') {
    description = 'Runs the sample builds.'
    group = 'verification'
    testClassesDir = sourceSets.samples.output.classesDir
    classpath = sourceSets.samples.runtimeClasspath

    reports {
        html.destination = project.file("$html.destination/samples")
        junitXml.destination = project.file("$junitXml.destination/samples")
    }
}

publishing {
    repositories {
        mavenLocal()
    }

    publications {
        plugin(MavenPublication) {
            from components.java
        }
    }
}

pluginBundle {
    website = 'https://github.com/rodm/gradle-teamcity-plugin'
    vcsUrl = 'https://github.com/rodm/gradle-teamcity-plugin'
    description = 'Gradle plugin for developing TeamCity plugins'
    tags = ['teamcity']

    plugins {
        teamcityServerPlugin {
            id = 'com.github.rodm.teamcity-server'
            displayName = 'Gradle TeamCity Server plugin'
        }
        teamcityAgentPlugin {
            id = 'com.github.rodm.teamcity-agent'
            displayName = 'Gradle TeamCity Agent plugin'
        }
        teamcityCommonPlugin {
            id = 'com.github.rodm.teamcity-common'
            displayName = 'Gradle TeamCity Common API plugin'
        }
    }

    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
    }
}
