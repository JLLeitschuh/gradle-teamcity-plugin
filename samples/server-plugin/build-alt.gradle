buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'com.github.rodm', name: 'gradle-teamcity-plugin', version: '0.8.3'
    }
}

apply plugin: 'java'
apply plugin: 'com.github.rodm.teamcity-server'

group = 'com.github.rodm.teamcity'
version = '1.0-SNAPSHOT'

def vendorName = 'rodm'

ext {
    teamcityVersion = '8.1.5'
    downloadsDir = project.hasProperty('downloads.dir') ? property('downloads.dir') : "$rootDir/downloads"
    serversDir = project.hasProperty('servers.dir') ? property('servers.dir') : "$rootDir/servers"
    java7Home = project.hasProperty('java7.home') ? property('java7.home') : '/opt/jdk1.7.0_80'
    java8Home = project.hasProperty('java8.home') ? property('java8.home') : '/opt/jdk1.8.0_74'
}

test {
    useTestNG()
}

teamcity {
    version = teamcityVersion

    descriptor = file("$rootDir/src/main/descriptor/teamcity-plugin.xml")
    tokens VERSION: project.version, VENDOR_NAME: vendorName


    downloadsDir = project.downloadsDir

    environments {
        teamcity81 {
            version = '8.1.5'
            homeDir = file("$serversDir/TeamCity-${version}")
            dataDir = file("$rootDir/data/8.1")
            javaHome = file(java7Home)
        }

        teamcity91 {
            version = '9.1.6'
            homeDir = file("$serversDir/TeamCity-${version}")
            dataDir = file("$rootDir/data/9.1")
            javaHome = file(java7Home)
        }

        teamcity10 {
            version = '10.0-EAP'
            downloadUrl = "https://download.jetbrains.com/teamcity/eap/TeamCity-40941.tar.gz"
            homeDir = file("$serversDir/TeamCity-${version}")
            dataDir = file("$rootDir/data/10.0")
            javaHome = file(java8Home)
        }
    }
}
