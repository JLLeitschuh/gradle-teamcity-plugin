
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'com.github.rodm', name: 'gradle-teamcity-plugin', version: '0.8.5'
    }
}

apply plugin: 'java'
apply plugin: 'com.github.rodm.teamcity-server'

group = 'com.github.rodm.teamcity'
version = '1.0-SNAPSHOT'

ext {
    vendorName = 'rodm'
    teamcityVersion = '8.1.5'

    downloadsDir = project.hasProperty('downloads.dir') ? property('downloads.dir') : "$rootDir/downloads"
    serversDir = project.hasProperty('servers.dir') ? property('servers.dir') : "$rootDir/servers"
    java7Home = project.hasProperty('java7.home') ? property('java7.home') : '/opt/jdk1.7.0_80'
    java8Home = project.hasProperty('java8.home') ? property('java8.home') : '/opt/jdk1.8.0_74'
}

test {
    useTestNG()
}

teamcity {
    version = teamcityVersion

    server {
        descriptor {
            name = project.name
            displayName = project.name
            version = project.version
            vendorName = project.vendorName
            description = 'TeamCity Example Server Plugin'

            downloadUrl = 'https://github.com/rodm/gradle-teamcity-plugin/'
            email = 'rod.n.mackenzie@gmail.com'
            useSeparateClassloader = false

            parameters {
                parameter 'param', 'value'
            }
        }

        downloadsDir = project.downloadsDir
        baseHomeDir = project.serversDir
        baseDataDir = 'data'

        environments {
            teamcity81 {
                version = '8.1.5'
                javaHome = file(java7Home)
            }

            teamcity91 {
                version = '9.1.6'
                javaHome = file(java7Home)
            }

            teamcity10 {
                version = '10.0-EAP'
                downloadUrl = "https://download.jetbrains.com/teamcity/eap/TeamCity-40941.tar.gz"
                homeDir = file("$serversDir/TeamCity-${version}")
                dataDir = file("$rootDir/data/10.0")
                javaHome = file(java8Home)
            }
        }
    }
}
