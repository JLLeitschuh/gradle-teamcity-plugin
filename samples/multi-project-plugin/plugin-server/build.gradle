
apply plugin: 'com.github.rodm.teamcity-server'

ext {
    downloadsDir = project.findProperty('downloads.dir') ?: "$rootDir/downloads"
    serversDir = project.findProperty('servers.dir') ?: "$rootDir/servers"
    java7Home = project.findProperty('java7.home') ?: '/opt/jdk1.7.0_80'
    java8Home = project.findProperty('java8.home') ?: '/opt/jdk1.8.0_92'
}

dependencies {
    agent project(path: ':plugin-agent', configuration: 'plugin')
    server project(':common')
    server project(':server')
}

teamcity {
    version = teamcityVersion

    server {
        descriptor {
            name = 'Example TeamCity Plugin'
            displayName = 'Example TeamCity Plugin'
            version = project.version
            vendorName = 'rodm'
            description = 'Example multi-project TeamCity plugin'
            email = 'rod.n.mackenzie@gmail.com'
            useSeparateClassloader = true
        }
    }

    environments {
        downloadsDir = project.downloadsDir
        baseHomeDir = project.serversDir
        baseDataDir = 'data'

        teamcity81 {
            version = '8.1.5'
            javaHome = file(java7Home)
        }

        teamcity91 {
            version = '9.1.7'
            javaHome = file(java7Home)
        }

        teamcity10 {
            version = '10.0.4'
            javaHome = file(java8Home)
        }

        teamcity2017 {
            version = '2017.1-EAP'
            downloadUrl = 'https://download.jetbrains.com/teamcity/eap/TeamCity-45965.tar.gz'
            javaHome = file(java8Home)
        }
    }
}
